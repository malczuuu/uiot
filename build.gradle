plugins {
    id("org.springframework.boot") version "${springBootPlugin}" apply false
    id("io.spring.dependency-management") version "${dependencyManagementPlugin}" apply false
    id("com.diffplug.spotless") version "${spotlessPlugin}" apply false
}

subprojects {
    apply plugin: "java"
    apply plugin: "com.diffplug.spotless"

    group = "io.github.malczuuu.uiot"

    /**
     * Conditional approach to versioning. If `-Pversion=...` is specified on the command line, it will be used as the
     * version. Otherwise, the version is derived from the git tag and build number.
     */
    if (version == null || version == "unspecified") {
        version = Versioning.getSnapshotVersion(rootProject.rootDir)
    }

    repositories {
        mavenCentral()
        mavenLocal()
        maven { url = "https://jitpack.io" }
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(17)
        }
    }

    spotless {
        format "misc", {
            target "*.gradle", ".gitattributes", ".gitignore"

            trimTrailingWhitespace()
            leadingTabsToSpaces(4)
            endWithNewline()
        }

        format "yaml", {
            target "**/*.yml", "**/*.yaml"

            trimTrailingWhitespace()
            leadingTabsToSpaces(2)
            endWithNewline()
        }

        java {
            target "src/**/*.java"

            googleJavaFormat("${googleJavaFormatVersion}")
        }
    }

    tasks.register("printVersion") {
        doLast {
            println "Project version: ${version}"
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.compilerArgs += ["-parameters"]
    }

    tasks.withType(Jar).configureEach {
        manifest {
            attributes "Implementation-Version": project.version
        }
    }

    tasks.withType(Test).configureEach {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    /**
     * Scripts that build Docker image require that there's only one jar.
     */
    pluginManager.withPlugin("org.springframework.boot") {
        tasks.withType(Jar).configureEach {
            if (name != "bootJar") {
                enabled = false
            }
        }
    }
}
